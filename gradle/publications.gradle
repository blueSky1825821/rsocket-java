apply from: "${rootDir}/gradle/github-pkg.gradle"
apply from: "${rootDir}/gradle/sonotype.gradle"

subprojects {
    plugins.withType(MavenPublishPlugin) {
        publishing {
            publications {
                maven(MavenPublication) {
                    pom {
                        name = project.name
                        afterEvaluate {
                            description = project.description
                        }
                        groupId = 'io.rsocket'
                        url = 'http://rsocket.io'
                        licenses {
                            license {
                                name = "The Apache Software License, Version 2.0"
                                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                                distribution = "repo"
                            }
                        }
                        developers {
                            developer {
                                id = 'OlegDokuka'
                                name = 'Oleh Dokuka'
                                email = 'oleh.dokuka@icloud.com'
                            }
                            developer {
                                id = 'rstoyanchev'
                                name = 'Rossen Stoyanchev'
                                email = 'rstoyanchev@vmware.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:https://github.com/rsocket/rsocket-java.git'
                            developerConnection = 'scm:git:https://github.com/rsocket/rsocket-java.git'
                            url = 'https://github.com/rsocket/rsocket-java'
                        }
                        versionMapping {
                            usage('java-api') {
                                fromResolutionResult()
                            }
                            usage('java-runtime') {
                                fromResolutionResult()
                            }
                        }
                    }
                }
            }
        }
    }
}